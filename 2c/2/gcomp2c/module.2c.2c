//Процедура ИнициализацияМодуля()
//	Предупреждение(ЗначениеАргументаПрограммы(0));
//КонецПроцедуры//ИнициализацияМодуля()
Перем ИмяФайлаМД;
Перем ИмяКаталогаМД;
Перем Мета;
Перем ТипФайлаМД;
///////////////////////////////////////////////////////////////////////
//Предопределенная процедура - вызывается при первом открытии формы
Процедура ПриОткрытии()
	//Разбор ключей программы
	// -d - декомпиляция 
	// -cf - компиляция внешней формы *.fd
	// -cm -компиляция md2
	// -F - имя файла md2 или fd с полным путем
	// -D - имя корневого каталога, содержащего цвс версию содержимого файла md2
	КолАргументов=КоличествоАргументовПрограммы();
	Мета1=СоздатьОбъект("Метаданные").Массив;
	//Если запущена из Предприятия, открываем форму
	Если РазмерСтруктуры(Мета1)>0 Тогда
		СписокДействий.ДобавитьЗначение(1,"Разборка");
		СписокДействий.ДобавитьЗначение(2,"Сборка");
		СписокДействий.ТекущаяСтрока(1);
		
		Форма.КнВыборФайла.Видимость(1);
		Форма.КнВыборКаталога.Видимость(0);
		Форма.КнВыборКаталога.Доступность(0);
		Форма.ИмяФайла.Видимость(1);
		Форма.ИмяКаталога.Видимость(0);
		Форма.КнВыборКаталога.Доступность(0);
		Форма.Надпись.Заголовок("Выберите файл:");
		ТипФайла=1;
		Возврат;
	КонецЕсли;	
	Аргумент=СоздатьОбъект("Массив");
	Если КолАргументов>2 Тогда
		Для А=1 По КолАргументов-2 Цикл
			Аргумент[А]=ЗначениеАргументаПрограммы(А+1);			
		КонецЦикла
		ФлагРазборки=0;
		ФлагСборки=0;
		ИмяКаталогаМД="";
		ИмяФайлаМД="";
        Для А=1 По КолАргументов-2 Цикл
			Если Аргумент[А]="-d" Тогда
				ФлагРазборки=1;
			ИначеЕсли Аргумент[А]="-cf" Тогда
				ФлагСборки=1;
				ТипФайлаМД=1;
			ИначеЕсли Аргумент[А]="-cm" Тогда
				ФлагСборки=1;
				ТипФайлаМД=2;	
			ИначеЕсли Лев(Аргумент[А],2)="-F" Тогда
				Если (Сред(Аргумент[А],3,1)="""") И
					 (Прав(Аргумент[А],1)="""") Тогда
				    ИмяФайлаМД=Сред(Аргумент[А],4,СтрДлина(Аргумент[А])-5);
				Иначе
					ИмяФайлаМД=Сред(Аргумент[А],3);	
				КонецЕсли;
			ИначеЕсли Лев(Аргумент[А],2)="-D" Тогда
				Если (Сред(Аргумент[А],3,1)="""") И
					 (Прав(Аргумент[А],1)="""") Тогда
				    ИмяКаталогаМД=Сред(Аргумент[А],4,СтрДлина(Аргумент[А])-5);
				Иначе
					ИмяКаталогаМД=Сред(Аргумент[А],3);	
				КонецЕсли		
			КонецЕсли					
        КонецЦикла		
        Если ФлагРазборки И ФлагСборки Тогда
	        Предупреждение("Ключи -d и -c нельзя задавать совместно!",20);
        	ЗавершитьРаботуСистемы(-1);
        КонецЕсли
        Если ФлагРазборки И ПустоеЗначение(ИмяФайлаМД)=0 Тогда
        	Разобрать();
        Иначе
	        Если ФлагСборки И ПустоеЗначение(ИмяКаталогаМД)=0 Тогда
	        	Собрать();
	        КонецЕсли
        КонецЕсли
		ЗавершитьРаботуСистемы(-1);
	КонецЕсли
	ЗавершитьРаботуСистемы(-1);
	//глВосстановитьЗначенияФормы(Контекст);
КонецПроцедуры

///////////////////////////////////////////////////////////////////////
//Предопределенная процедура - вызывается при закрытии формы
Процедура ПриЗакрытии()
//	Если СостояниеКлавиши(27) Тогда
//		СтатусВозврата(0);
//	КонецЕсли;
//	глЗапомнитьЗначенияФормы(Контекст);
КонецПроцедуры

///////////////////////////////////////////////////////////////////////
//Процедура обработки нажатия кнопки Закрыть - выполняет закрытие формы
Процедура ЗакрытьФорму()
	Форма.Закрыть();
КонецПроцедуры

///////////////////////////////////////////////////////////////////////
//Процедура обработки нажатия кнопки Разобрать
Процедура Разобрать()
	Если ПустоеЗначение(ИмяФайлаМД)=0 Тогда
		ИмяФайла=ИмяФайлаМД;
	КонецЕсли

    Мета=СоздатьОбъект("ФайлМетаданных"); 
    Мас=СоздатьОбъект("Массив");
    
	Мета.Открыть(ИмяФайла,1); 
	ФС=СоздатьОбъект("Файловая система");
	Если Прав(ИмяФайла,4)=".md2" Тогда
		ИмяКаталога=Лев(ИмяФайла,СтрДлина(ИмяФайла)-4);
	ИначеЕсли Прав(ИмяФайла,3)=".fd" Тогда
		ИмяКаталога=Лев(ИмяФайла,СтрДлина(ИмяФайла)-3);
	Иначе
		Предупреждение("Обработка может разбирать только файлы (*.md2) и (*.fd).Выберите другой файл.",20);
		Возврат;		
	КонецЕсли
	ФС.СоздатьКаталог(ИмяКаталога);
	//Мета.ВыбратьФайлы();
	Кол=Мета.КоличествоЭлементов();
	Для А=1 По Кол Цикл 
    	Мас[А]=Мета.ПолучитьИмя(А);
    КонецЦикла;
    Мас.Сортировать();
    Для А=1 По РазмерМассива(Мас) Цикл
        Имя=Мас[А]; 
        Стр=Мета.ПрочитатьТекст(Имя);
    	Стр=СтрЗаменить(Стр,Симв(13),""); 
    	Имя1=СтрЗаменить(Имя," ","__");
	    Если Прав(Имя,4)=".atr" Тогда
	    ИначеЕсли Прав(Имя,4)=".inf" Тогда
		ИначеЕсли Прав(Имя,4)=".frm" Тогда
	    ИначеЕсли Прав(Имя,1)="\" Тогда
		    Продолжить;
	    Иначе
		    Если Лев(Стр,5)="{\rtf" Тогда
			    Имя1=Имя1+".rtf";
		    ИначеЕсли Лев(Стр,5)="MYMXL" Тогда
			    //не буду пока связываться с таблицами вообще 
				Имя2=Имя1+".mxl";
//				ИмяФайлаТек=Лев(ИмяФайла,СтрДлина(ИмяФайла)-4)+"\"+Имя2;
//				Данные=Мета.ПрочитатьДанные(Имя);
// 				Объект=Мета.ПрочитатьОбъект(Имя);
// 				Сообщить(ТипЗначенияСтр(Данные));
// 				Сообщить(ТипЗначенияСтр(Объект));
// 				СтрКоманды=КаталогПрограммы()+"7z.exe";
// 				СтрПараметров="e "+ИмяФайла+" """+Имя+""" -o"+ИмяКаталога; 
//                ЗапуститьПриложение(СтрКоманды,СтрПараметров);
//                Пока ФС.СуществуетФайл(Лев(ИмяФайла,СтрДлина(ИмяФайла)-4)+"\"+Имя1)=0 Цикл
//                	Пауза(100);
//                КонецЦикла
//                ФС.ПереименоватьФайл(Лев(ИмяФайла,СтрДлина(ИмяФайла)-4)+"\"+Имя1,ИмяФайлаТек); 
				Продолжить;  
			Иначе       
		    	Имя1=Имя1+".2c";   
		    КонецЕсли
	    КонецЕсли
	    ИмяФайлаТек=ИмяКаталога+"\"+Имя1;
 		Текст=СоздатьОбъект("Текст");
 		Текст.ДобавитьСтроку(Стр);
 		ИмяКаталога1=ВыделитьИмяКаталога(ИмяФайлаТек);
 		ФС.СоздатьКаталог(ИмяКаталога1);
 		Текст.Записать(ИмяФайлаТек);
	КонецЦикла
КонецПроцедуры//Разобрать

//Процедура обработки нажатия кнопки <...> - выполняет выбор файла
Процедура ПриВыбореФайла()
	ИмяФайлаПоУмолчанию=ИмяФайла;
    ФС=СоздатьОбъект("Файловая система");
    ФС.ВыбратьФайл(0,ИмяФайлаПоУмолчанию,,"Выберите файл:","Файлы (*.md2),(*.fd)|*.?d*");
    ИмяФайла=ИмяФайлаПоУмолчанию;
КонецПроцедуры

Функция ВыделитьИмяКаталога(Знач ИмяФ)
	ИмяКаталога2="";
	Поз=Найти(ИмяФ,"\");
	Пока Поз>0 Цикл
		ИмяКаталога2=ИмяКаталога2+Сред(ИмяФ,1,Поз);
		ИмяФ=Сред(ИмяФ,Поз+1);
		Поз=Найти(ИмяФ,"\");
	КонецЦикла
    Возврат ИмяКаталога2;
КонецФункции //ВыделитьИмяКаталога(ИмяФ)

///////////////////////////////////////////////////////////////////////
Процедура Собрать()
	Если ТипФайлаМД>0 Тогда
		ТипФайла1=ТипФайлаМД;
	Иначе
		ТипФайла1=ТипФайла;	
	КонецЕсли

	ТипФайла=ТипФайла1;
	Если ПустоеЗначение(ИмяКаталогаМД)=0 Тогда
		ИмяКаталога=ИмяКаталогаМД;
	КонецЕсли
    Мета=СоздатьОбъект("ФайлМетаданных"); 
    ФС=СоздатьОбъект("Файловая система");
	Если ТипФайла1=2 Тогда
        ИмяФайла=Лев(ИмяКаталога,СтрДлина(ИмяКаталога)-1)+".md2";
    ИначеЕсли ТипФайла1=1 Тогда
	    ИмяФайла=Лев(ИмяКаталога,СтрДлина(ИмяКаталога)-1)+".fd";
    КонецЕсли
   
    Если ФС.СуществуетФайл(ИмяФайла) Тогда
    	Предупреждение("Существующий файл "+ИмяФайла+" будет переименован в файл с расширением .!md2",5);
    	ИмяФайла1=Лев(ИмяФайла,СтрДлина(ИмяФайла)-4)+".!md2";
    	ФС.ПереименоватьФайл(ИмяФайла,ИмяФайла1,1);
    КонецЕсли
    Мета.Открыть(ИмяФайла);
    РекурсивнаяСборка("");
 	Мета.Закрыть();
КонецПроцедуры//Собрать

//Процедура обработки нажатия кнопки <...> - выполняет выбор каталога
Процедура ПриВыбореКаталога()
	ИмяКаталогаПоУмолчанию=ИмяКаталога;
    ФС=СоздатьОбъект("Файловая система");
    ФС.ВыбратьКаталог(ИмяКаталогаПоУмолчанию,"Выберите каталог для сборки MD2:");
    ИмяКаталога=ИмяКаталогаПоУмолчанию;
КонецПроцедуры

Процедура РекурсивнаяСборка(Путь)
	ФС=СоздатьОбъект("Файловая система");
	СписокФайлов=СоздатьОбъект("СписокЗначений");
	ФС.УстТекКаталог(ИмяКаталога+Путь);
	ТекФайл=ФС.НайтиПервыйФайл("*.*");
	Пока ПустоеЗначение(ТекФайл)=0 Цикл
		СписокФайлов.ДобавитьЗначение(ТекФайл);
		ТекФайл=ФС.НайтиСледующийФайл();
	КонецЦикла;	
	Для А=1 По СписокФайлов.РазмерСписка() Цикл
		ТекФайл=СписокФайлов.ПолучитьЗначение(А);
		Если (ТекФайл=".") ИЛИ (ТекФайл="..") Тогда
		   	Продолжить;
		ИначеЕсли Прав(ТекФайл,4)=".atr" Тогда
			Имя=ТекФайл;
		ИначеЕсли Прав(ТекФайл,4)=".inf" Тогда
			Имя=ТекФайл;
		ИначеЕсли Прав(ТекФайл,4)=".frm" Тогда
			Имя=ТекФайл;
		ИначеЕсли Прав(ТекФайл,4)=".rtf" Тогда
			Имя=Лев(ТекФайл,СтрДлина(ТекФайл)-4);
		ИначеЕсли Прав(ТекФайл,4)=".mxl" Тогда
//			Имя=Лев(ТекФайл,СтрДлина(ТекФайл)-4);
            Продолжить; 		
		ИначеЕсли Прав(ТекФайл,3)=".2c" Тогда
			Имя=Лев(ТекФайл,СтрДлина(ТекФайл)-3);
		ИначеЕсли Прав(ТекФайл,3)="CVS" Тогда
			Продолжить;
		Иначе
			//считаем, что каталог
		    РекурсивнаяСборка(Путь+ТекФайл+"\");
		    Продолжить;
    	КонецЕсли
    	ПутьИмя=СтрЗаменить(Путь+Имя,"__"," ");
    	Текст=СоздатьОбъект("Текст");
    	Текст.Открыть(ИмяКаталога+Путь+ТекФайл);
    	КолСтрок=Текст.КоличествоСтрок();
        Стр=""; 
    	Если КолСтрок>0 Тогда
	    	Для Б=1 По КолСтрок Цикл
		    	Если Б=1 Тогда
		    		Стр=Текст.ПолучитьСтроку(Б);
		    	Иначе
			    	Стр=Стр+Симв(13)+Симв(10)+Текст.ПолучитьСтроку(Б);	
		    	КонецЕсли
	    	КонецЦикла
    	КонецЕсли
        Мета.ЗаписатьТекст(ПутьИмя,Стр);
 	КонецЦикла
КонецПроцедуры//РекурсивнаяСборка(Путь)

Процедура Выполнить()
	Действие=СписокДействий.ТекущаяСтрока();
	Если Действие=1 Тогда
		Разобрать();
	Иначе
		Собрать();	
	КонецЕсли
КонецПроцедуры//Выполнить

Процедура ПриВыбореДействия()
	Действие=СписокДействий.ТекущаяСтрока();
	Если Действие=1 Тогда
		Форма.КнВыборФайла.Видимость(1);
		Форма.КнВыборФайла.Доступность(1);
		Форма.КнВыборКаталога.Видимость(0);
		Форма.КнВыборКаталога.Доступность(0);
		Форма.ИмяФайла.Видимость(1);
		Форма.ИмяФайла.Доступность(1);
		Форма.ИмяКаталога.Видимость(0);
		Форма.ИмяКаталога.Доступность(0);
		Форма.Надпись.Заголовок("Выберите файл:");
	Иначе
		Форма.КнВыборФайла.Видимость(0);
		Форма.КнВыборФайла.Доступность(0);
		Форма.КнВыборКаталога.Видимость(1);
		Форма.КнВыборКаталога.Доступность(1);
		Форма.ИмяФайла.Видимость(0);
		Форма.ИмяФайла.Доступность(0);
		Форма.ИмяКаталога.Видимость(1);
		Форма.ИмяКаталога.Доступность(1);
		Форма.Надпись.Заголовок("Выберите каталог:");	
	КонецЕсли

КонецПроцедуры//ПриВыбореДействия
