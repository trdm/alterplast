//Процедура ИнициализацияМодуля()
//	Предупреждение(ЗначениеАргументаПрограммы(0));
//КонецПроцедуры//ИнициализацияМодуля()
Перем ИмяФайлаПр;
Перем ИмяКаталогаМД;
Перем Мета;
Перем ТипФайлаМД;
///////////////////////////////////////////////////////////////////////
//Предопределенная процедура - вызывается при первом открытии формы
Процедура ПриОткрытии()
	//Разбор ключей программы
	// <ИмяФайла>.pr - файл проекта 
	// -D - имя корневого каталога, содержащего цвс версию содержимого файла md2
	// будет сформирован файл <ИмяФайла>.md2 согласно указаниям в файле проекта
	// в файле проекта указаны каталоги относительно корня репозитария, которые будут включены в библиотеку
	КолАргументов=КоличествоАргументовПрограммы();
	Мета1=СоздатьОбъект("Метаданные").Массив;
	//Если запущена из Предприятия, открываем форму
	Если РазмерСтруктуры(Мета1)>0 Тогда
	
		Возврат;
	КонецЕсли;	
	Аргумент=СоздатьОбъект("Массив");
	Если КолАргументов>2 Тогда
		Для А=1 По КолАргументов-2 Цикл
			Аргумент[А]=ЗначениеАргументаПрограммы(А+1);			
		КонецЦикла
		ФлагСборки=0;
		ИмяКаталогаМД="";
		ИмяФайлаМД="";
        Для А=1 По КолАргументов-2 Цикл
			Если Лев(Аргумент[А],2)="-D" Тогда
				Если (Сред(Аргумент[А],3,1)="""") И
					 (Прав(Аргумент[А],1)="""") Тогда
				    ИмяКаталогаМД=Сред(Аргумент[А],4,СтрДлина(Аргумент[А])-5);
				Иначе
					ИмяКаталогаМД=Сред(Аргумент[А],3);	
				КонецЕсли		
			ИначеЕсли Прав(Аргумент[А],3)=".pr" Тогда
			    ИмяФайлаПр=Аргумент[А]; 
			КонецЕсли					
        КонецЦикла		
        Собрать();
        ЗавершитьРаботуСистемы(-1);
	КонецЕсли
	ЗавершитьРаботуСистемы(-1);
	//глВосстановитьЗначенияФормы(Контекст);
КонецПроцедуры

///////////////////////////////////////////////////////////////////////
//Предопределенная процедура - вызывается при закрытии формы
Процедура ПриЗакрытии()
//	Если СостояниеКлавиши(27) Тогда
//		СтатусВозврата(0);
//	КонецЕсли;
//	глЗапомнитьЗначенияФормы(Контекст);
КонецПроцедуры

///////////////////////////////////////////////////////////////////////
//Процедура обработки нажатия кнопки Закрыть - выполняет закрытие формы
Процедура ЗакрытьФорму()
	Форма.Закрыть();
КонецПроцедуры


//Процедура обработки нажатия кнопки <...> - выполняет выбор файла
Процедура ПриВыбореФайла()
	ИмяФайлаПоУмолчанию=ИмяФайлаПр1;
    ФС=СоздатьОбъект("Файловая система");
    ФС.ВыбратьФайл(0,ИмяФайлаПоУмолчанию,,"Выберите файл:","Файлы (*.pr)|*.pr");
    ИмяФайлаПр1=ИмяФайлаПоУмолчанию;
    ИмяФайлаПр=ИмяФайлаПр1;
КонецПроцедуры

Функция ВыделитьИмяКаталога(Знач ИмяФ)
	ИмяКаталога2="";
	Поз=Найти(ИмяФ,"\");
	Пока Поз>0 Цикл
		ИмяКаталога2=ИмяКаталога2+Сред(ИмяФ,1,Поз);
		ИмяФ=Сред(ИмяФ,Поз+1);
		Поз=Найти(ИмяФ,"\");
	КонецЦикла
    Возврат ИмяКаталога2;
КонецФункции //ВыделитьИмяКаталога(ИмяФ)

Функция ВыделитьИмяФайла(Знач ИмяФ)
	ИмяФайла2=ИмяФ
	Поз=Найти(ИмяФайла2,"\");
	Пока Поз>0 Цикл
		ИмяФайла2=Сред(ИмяФайла2,Поз+1);
		Поз=Найти(ИмяФайла2,"\");
	КонецЦикла
	Возврат ИмяФайла2;
КонецФункции //ВыделитьИмяФайла(ИмяФ)

///////////////////////////////////////////////////////////////////////
Процедура Собрать()
	Если ПустоеЗначение(ИмяКаталогаМД)=0 Тогда
		ИмяКаталога=ИмяКаталогаМД;
	КонецЕсли
    Мета=СоздатьОбъект("ФайлМетаданных"); 
    ФС=СоздатьОбъект("Файловая система");
    
	ИмяФайла=ВыделитьИмяКаталога(Лев(ИмяКаталога,СтрДлина(ИмяКаталога)-1))+ВыделитьИмяФайла(Лев(ИмяФайлаПр,СтрДлина(ИмяФайлаПр)-3)+".md2");
    
    Если ФС.СуществуетФайл(ИмяФайла) Тогда
    	Предупреждение("Существующий файл "+ИмяФайла+" будет переименован в файл с расширением .!md2",5);
    	ИмяФайла1=Лев(ИмяФайла,СтрДлина(ИмяФайла)-4)+".!md2";
    	ФС.ПереименоватьФайл(ИмяФайла,ИмяФайла1,1);
    КонецЕсли
    Мета.Открыть(ИмяФайла);
    Проект=СоздатьОбъект("Текст");
    Проект.Открыть(ИмяФайлаПр);
    ПрКолСтрок=Проект.КоличествоСтрок();
    Для Сч=1 По ПрКолСтрок Цикл
    	Стр=Проект.ПолучитьСтроку(Сч);
    	Если ПустаяСтрока(Стр)=1 Тогда
    	    Продолжить
    	ИначеЕсли Лев(Стр,2)="//" Тогда
    	//Это комментарий  
    	    Продолжить    
    	Иначе    
    		РекурсивнаяСборка(Стр);
    	КонецЕсли;
    КонецЦикла
    Мета.Закрыть();
КонецПроцедуры//Собрать

//Процедура обработки нажатия кнопки <...> - выполняет выбор каталога
Процедура ПриВыбореКаталога()
	ИмяКаталогаПоУмолчанию=ИмяКаталога;
    ФС=СоздатьОбъект("Файловая система");
    ФС.ВыбратьКаталог(ИмяКаталогаПоУмолчанию,"Выберите каталог для сборки MD2:");
    ИмяКаталога=ИмяКаталогаПоУмолчанию;
КонецПроцедуры

Процедура РекурсивнаяСборка(Путь)
	Если ПустоеЗначение(Путь)=1 Тогда
		Возврат
	КонецЕсли;	
	ФС=СоздатьОбъект("Файловая система");
	СписокФайлов=СоздатьОбъект("СписокЗначений");
	ЭтоКаталог=0;
	Если Прав(Путь,1)="\" Тогда
		ФС.УстТекКаталог(ИмяКаталога+Путь);
		ТекФайл=ФС.НайтиПервыйФайл("*.*");
		Пока ПустоеЗначение(ТекФайл)=0 Цикл
			СписокФайлов.ДобавитьЗначение(ТекФайл);
			ТекФайл=ФС.НайтиСледующийФайл();
		КонецЦикла;
		ЭтоКаталог=1;
	Иначе
	    ИмяФайлаТек=ВыделитьИмяФайла(Путь);
	    Путь=ВыделитьИмяКаталога(Путь);
	    ФС.УстТекКаталог(ИмяКаталога+Путь);
	    Если ФС.СуществуетФайл(ИмяКаталога+Путь+ИмяФайлаТек+".inf") Тогда
	    	СписокФайлов.ДобавитьЗначение(ИмяФайлаТек+".inf");
	    КонецЕсли;		
	    Если ФС.СуществуетФайл(ИмяКаталога+Путь+ИмяФайлаТек+".2c") Тогда
	    	СписокФайлов.ДобавитьЗначение(ИмяФайлаТек+".2c");
	    КонецЕсли;		
	    Если ФС.СуществуетФайл(ИмяКаталога+Путь+ИмяФайлаТек+".frm") Тогда
	    	СписокФайлов.ДобавитьЗначение(ИмяФайлаТек+".frm");
	    КонецЕсли;		
	    Если ФС.СуществуетФайл(ИмяКаталога+Путь+ИмяФайлаТек+".rtf") Тогда
	    	СписокФайлов.ДобавитьЗначение(ИмяФайлаТек+".rtf");
	    КонецЕсли;		
	КонецЕсли
	Для А=1 По СписокФайлов.РазмерСписка() Цикл
		ТекФайл=СписокФайлов.ПолучитьЗначение(А);
		Если (ТекФайл=".") ИЛИ (ТекФайл="..") Тогда
		   	Продолжить;
		ИначеЕсли Прав(ТекФайл,4)=".atr" Тогда
			Имя=ТекФайл;
		ИначеЕсли Прав(ТекФайл,4)=".inf" Тогда
			Имя=ТекФайл;
		ИначеЕсли Прав(ТекФайл,4)=".frm" Тогда
			Имя=ТекФайл;
		ИначеЕсли Прав(ТекФайл,4)=".rtf" Тогда
			Имя=Лев(ТекФайл,СтрДлина(ТекФайл)-4);
		ИначеЕсли Прав(ТекФайл,4)=".mxl" Тогда
//			Имя=Лев(ТекФайл,СтрДлина(ТекФайл)-4);
            Продолжить; 		
		ИначеЕсли Прав(ТекФайл,3)=".2c" Тогда
			Имя=Лев(ТекФайл,СтрДлина(ТекФайл)-3);
		ИначеЕсли Прав(ТекФайл,3)="CVS" Тогда
			Продолжить;
		Иначе
			//считаем, что каталог
		    РекурсивнаяСборка(Путь+ТекФайл+"\");
		    Продолжить;
    	КонецЕсли
    	ПутьИмя=СтрЗаменить(Путь+Имя,"__"," ");
    	Текст=СоздатьОбъект("Текст");
    	Текст.Открыть(ИмяКаталога+Путь+ТекФайл);
    	КолСтрок=Текст.КоличествоСтрок();
        Стр=""; 
    	Если КолСтрок>0 Тогда
	    	Для Б=1 По КолСтрок Цикл
		    	Если Б=1 Тогда
		    		Стр=Текст.ПолучитьСтроку(Б);
		    	Иначе
			    	Стр=Стр+Симв(13)+Симв(10)+Текст.ПолучитьСтроку(Б);	
		    	КонецЕсли
	    	КонецЦикла
    	КонецЕсли
        Мета.ЗаписатьТекст(ПутьИмя,Стр);
 	КонецЦикла
 	Если ЭтоКаталог=1 Тогда
 	    РекурсивнаяСборка(Лев(Путь,СтрДлина(Путь)-1));
 	КонецЕсли;    
КонецПроцедуры//РекурсивнаяСборка(Путь)

Процедура Выполнить()
	Собрать();
КонецПроцедуры//Выполнить
