#Задать ИмяФайла КаталогПользователя()+"SQL_Сервис.lst"//
Перем ТекИнфо;
///////////////////////////////////////////////////////////////////////
//Предопределенная процедура - вызывается при первом открытии формы
Процедура ПриОткрытии()
	СохраненныеЗначения=0;
	ЗначениеИзФайла(ИмяФайла,СохраненныеЗначения);
	Если ТипЗначенияСтр(СохраненныеЗначения)="Массив" Тогда
		ТекстЗапроса=СохраненныеЗначения.ТекстЗапроса;
		СохраненныеЗначения.СписокЗапросов.Выгрузить(СписокЗапросов);
	КонецЕсли
	ТаблицаРезультата.УстановитьШиринуКолонки(0,40);
КонецПроцедуры

///////////////////////////////////////////////////////////////////////
//Предопределенная процедура - вызывается при закрытии формы
Процедура ПриЗакрытии()
	СохраненныеЗначения=СоздатьОбъект("Массив");
	СохраненныеЗначения.ТекстЗапроса=ТекстЗапроса;
	СохраненныеЗначения.СписокЗапросов=СписокЗапросов;
	ЗначениеВФайл(ИмяФайла,СохраненныеЗначения);
КонецПроцедуры

///////////////////////////////////////////////////////////////////////
//Процедура обработки нажатия кнопки Закрыть - выполняет закрытие формы
Процедура ЗакрытьФорму()
	Форма.Закрыть();
КонецПроцедуры


Процедура Выполнить()
//	СохранитьТекст();
	Запрос(ТекстЗапроса);
КонецПроцедуры

///////////////////////////////////////////////////////////////////////
//Процедура обработки нажатия кнопки Выполнить
Процедура Запрос(ТекстТекущегоЗапроса)
	Перем МасДлинКолонок[150];//для автоопределения ширины колонки таблицы

	СохранитьТекст(ТекстТекущегоЗапроса);
	
	ТаблицаРезультата.КоличествоКолонок(1);
	ТаблицаРезультата.КоличествоСтрок(1);
	
	Если Сокрлп(ТекстТекущегоЗапроса)="" Тогда
		Возврат;
	КонецЕсли
	
	
	Время1=_GetPerformanceCounter();
	//Сообщить(ТипБазыДанных);
	БД=СоздатьОбъект("БазаДанных");
	БылоСоединение=0;
	//выполнение запроса
	Состояние("Выполнение запроса");
	Попытка
		Рез=БД.Запрос(ТекстТекущегоЗапроса);
	Исключение
		Сообщить("Ошибка в запросе: "+ОписаниеОшибки());
		Возврат;
	КонецПопытки
	Время2=_GetPerformanceCounter();
	ТекИнфо=""+(Время2-Время1)+" мс.";
	
	Если Рез=0 Тогда
		Возврат;
	КонецЕсли
		

	
		
	ТаблицаРезультата.ЗадатьТекст(0,0,"№");
	ТаблицаРезультата.УстановитьШиринуКолонки(0,40);

	ТаблицаРезультата.КоличествоКолонок(БД.КоличествоПолей()+1);
	

	
	
	//возвращаемые поля
	Состояние("Обработка запроса");
	МасИмен=БД.ПолучитьИменаПолей();
	Для А=1 По Разм(МасИмен) Цикл
		МасДлинКолонок[А]=СтрДлина(Сокрп(МасИмен[А]));
		ТаблицаРезультата.ЗадатьТекст(0,А,МасИмен[А]);
		//ТаблицаРезультата.УстановитьШиринуКолонки(А,200);
	КонецЦикла
	
	Ном=0;
	Пока  БД.ПолучитьСтроку() Цикл
		Ном=Ном+1;
		ТаблицаРезультата.КоличествоСтрок(Ном+1);
		МасЗначений=БД.ПолучитьЗначенияПолей();
	
		ТаблицаРезультата.ЗадатьТекст(Ном,0,Ном);
		Для А=1 По Разм(МасЗначений) Цикл
			МасДлинКолонок[А]=Макс(МасДлинКолонок[А],СтрДлина(Сокрп(МасЗначений[А])));
			ТаблицаРезультата.ЗадатьТекст(Ном,А,МасЗначений[А]);
		КонецЦикла
	КонецЦикла
	Для А=1 По Разм(МасИмен) Цикл
		ТаблицаРезультата.УстановитьШиринуКолонки(А,Макс(4,МасДлинКолонок[А])*10);
	КонецЦикла
	ТаблицаРезультата.Обновить();
	Состояние("");
КонецПроцедуры

Процедура ДобавитьИмяТабл()
	ИмяТаблицы=ПолучитьИмяТабл();
	Если Сокрлп(ИмяТаблицы)="" Тогда
		Возврат;
	КонецЕсли;
	ТекстЗапроса=ТекстЗапроса+" "+ИмяТаблицы;
КонецПроцедуры

Функция ПолучитьИмяТабл()
	БД=СоздатьОбъект("БазаДанных");
	Попытка
	Рез=БД.Запрос("SHOW TABLES");
	Исключение
	Рез=БД.Запрос("SELECT name FROM SQLITE_MASTER WHERE type='table' ORDER BY name");
	КонецПопытки
	
	Список=СоздатьОбъект("СписокЗначений");
	ТекТаб="";
	Если Рез=1 Тогда
		Пока  БД.ПолучитьСтроку() Цикл
			Список.ДобавитьЗначение(БД.ПолучитьПоле(1));
		КонецЦикла
		Если Список.РазмерСписка()>15 Тогда
			Рез=Список.ВыбратьЗначение(ТекТаб,"","","",0);
			Иначе
			Рез=Список.ВыбратьЗначение(ТекТаб,"","","",1);
		КонецЕсли;
		Если Рез<>1 Тогда
			ТекТаб="";
		КонецЕсли;
	КонецЕсли
	Возврат ТекТаб;
КонецФункции

//_____________________________________________________________________________
Процедура ВывестиТаблицу()
	ИмяТаблицы=ПолучитьИмяТабл();
	Если Сокрлп(ИмяТаблицы)="" Тогда
		Возврат;
	КонецЕсли;
	СтрОграничения="";
	Запрос("SELECT  * FROM "+ИмяТаблицы+СтрОграничения);
	ИмяТаблицы="";
КонецПроцедуры //ВывестиТаблицу


Процедура ОчиститьТаблицу()
	ИмяТаблицы=ПолучитьИмяТабл();
	Если Сокрлп(ИмяТаблицы)="" Тогда
		Возврат;
	КонецЕсли;
	БД=СоздатьОбъект("БазаДанных");
	БД.Запрос("DELETE FROM "+ИмяТаблицы);
КонецПроцедуры

//_____________________________________________________________________________
Процедура УдалитьТаблицу()
	ИмяТаблицы=ПолучитьИмяТабл();
	Если Сокрлп(ИмяТаблицы)="" Тогда
		Возврат;
	КонецЕсли;
	БД=СоздатьОбъект("БазаДанных");
	БД.Запрос("DROP TABLE "+ИмяТаблицы);
КонецПроцедуры //УдалитьТаблицу


//_____________________________________________________________________________
Процедура ВставитьЗапрос()
	Ном=СписокЗапросов.ТекущаяСтрока();
	Если Ном>0 Тогда
		ТекстЗапроса=СписокЗапросов.ПолучитьЗначение(Ном);
		Если нрег(лев(ТекстЗапроса,6))="select" ИЛИ нрег(лев(ТекстЗапроса,4))="show" Тогда
			Запрос(ТекстЗапроса);
		КонецЕсли
	КонецЕсли;
КонецПроцедуры //ВставитьЗапрос

//_____________________________________________________________________________
Процедура СохранитьТекст(Стр="")
	Если Стр="" Тогда
		 Стр=ТекстЗапроса;
	КонецЕсли
	
	Рез=СписокЗапросов.НайтиЗначение(Стр);
	Если Рез=0 Тогда
		СписокЗапросов.ВставитьЗначение(1,Стр);
	Иначе
		//сдвигаем вверх
		СписокЗапросов.СдвинутьЗначение(-Рез,Рез);
	КонецЕсли;
	Пока СписокЗапросов.РазмерСписка()>500 Цикл
		СписокЗапросов.УдалитьЗначение(501);
	КонецЦикла;
КонецПроцедуры //СохранитьТекст

Процедура ПоказатьСтатусТаблицы()
	Запрос("SHOW TABLE STATUS");
КонецПроцедуры


Процедура ПоказатьПоляТаблицы()
	ИмяТаблицы=ПолучитьИмяТабл();
	Если Сокрлп(ИмяТаблицы)="" Тогда
		Возврат;
	КонецЕсли;
	Запрос("SHOW FIELDS FROM "+ИмяТаблицы);
КонецПроцедуры


ТекстЗапроса="SHOW TABLES";